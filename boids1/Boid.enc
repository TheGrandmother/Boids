passive class BoidData {
  location: Vector2D
  velocity: Vector2D
  acceleration: Vector2D
  r: real
  maxforce: real -- Maximum steering force
  maxspeed: real -- Maximum speed

  def init(loc: Vector2D): void {
    this.location = loc;
    this.acceleration = new Vector2D(0.0, 0.0);
    let angle = randomReal(0, 6.28);
    this.velocity = new Vector2D(cos(angle), sin(angle));
    this.r = 2.0;
    this.maxspeed = 2.0;
    this.maxforce = 0.03;
  }

  def clone(): BoidData {
    null -- TODO
  }
}

class Boid {
  id: int
  data: BoidData
  width: int
  height: int

  def init(id: int, loc: Vector2D, width: int, height: int): void {
    this.id = id;
    this.data = new BoidData(loc);
    this.width = width;
    this.height = height;
  }

  def move(flock : Flock, boids: [BoidData]): void {
    -- the BoidData for the result
    let sendable = this.data.clone();
    -- calculation
    let sep = this.separation(boids);
    let ali = this.align(boids);
    let coh = this.cohesion(boids);
    -- Arbitrarily weight these forces
    let weightedSep = sep.mul(1.5);
    let weightedAli = ali.mul(1.0);
    let weightedCoh = coh.mul(1.0);
    -- Add the force vectors to acceleration
    sendable.acceleration = sendable.acceleration.add(weightedSep).
                              add(weightedAli).
                              add(weightedCoh);
    -- Update velocity
    sendable.velocity = sendable.velocity.add(sendable.acceleration).
                          limit(sendable.maxspeed); -- Limit speed

    sendable.location = sendable.location.add(sendable.velocity);

    -- Reset accelertion to 0 each cycle
    sendable.acceleration = sendable.acceleration.mul(0);

    this.borders(sendable);

    flock.ack(sendable, this.id);
  }

  def separation(boids: [BoidData]): Vector2D {
    let desiredSeparation = 25.0;
    let steer = new Vector2D(0.0, 0.0);
    let count = 0;

    -- For every boid in the system, check if it's too close
    for other in boids {
      let dist = this.data.location.distance(other.location);
      if (dist > 0) and (dist < desiredSeparation) then {
        let diff = this.data.location.sub(other.location);
        let normalizedDiff = diff.normalize().div(dist);
        steer = steer.add(normalizedDiff);
        count = count + 1
      }
    };
    if (count > 0) then
      steer = steer.div(count);
    if steer.magnitude() > 0 then {
      steer.normalize().
        mul(this.data.maxspeed).
        sub(this.data.velocity).
        limit(this.data.maxforce)
    } else {
      steer
    }
  }

  def seek(target: Vector2D): Vector2D{
    let desired = target.sub(this.data.location);
    let normalizedDesired = desired.normalize().mul(this.data.maxspeed);
    normalizedDesired.sub(this.data.velocity).limit(this.data.maxforce);
  }
  -- Alignment
  -- For every nearby boid in the system, calculate the average velocity
  def align (boids: [BoidData]) : Vector2D {
    let neighbordist = 50;
    let sum = new Vector2D(0.0, 0.0);
    let count = 0;
    for other in boids {
      let d = sum.distance(other.location);
      if (d > 0) and (d < neighbordist) then {
        sum = sum.add(other.velocity);
        count = count + 1;
      }
    };
    if (count > 0) then {
      sum.div(count*1.0).
        normalize().
        mul(this.data.maxspeed).
        sub(this.data.velocity).
        limit(this.data.maxforce);
    }
    else {
      new Vector2D(0.0, 0.0);
    }
  }

  def cohesion(boids: [BoidData]) : Vector2D {
    let neighborDistance = 50.0;
    let location = this.data.location;
    let sum = new Vector2D(0.0, 0.0);
    let count = 0;
    for other in boids {
      let d = location.distance(other.location);
      if (d > 0) and (d < neighborDistance) then {
        sum = sum.add(other.location);
        count = count + 1;
      }
    };
    if (count > 0) then {
      this.seek(sum.div(count));
    } else {
      new Vector2D(0, 0);
    }
  }

  def borders(sendable: BoidData): void {
    let x = sendable.location.x;
    let y = sendable.location.y;
    if x < (0-this.data.r) then {
      x = this.width + this.data.r;
    } else if x > this.width + this.data.r then {
      x = 0-this.data.r;
    };

    if y < (0-this.data.r) then {
      y = this.height + this.data.r;
    } else if y > (this.height+this.data.r) then {
      y = 0-this.data.r;
    };

    sendable.location = new Vector2D(x, y);
  }
}
